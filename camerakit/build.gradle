plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'com.jfrog.bintray' version '1.7.3'
    id 'com.github.dcendents.android-maven' version '1.5'
}

android {
    compileSdkVersion 28
    defaultConfig {
        minSdkVersion 15
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
}

repositories {
    google()
    jcenter()
}

dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.2.50'
    implementation 'com.android.support:support-annotations:27.1.1'
    implementation 'com.camerakit:jpegkit:0.2.0-alpha1'
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:1.10.19'
}

ext.getBintrayUser = { ->
    if (System.getenv('BINTRAY_USER')) {
        return System.getenv('BINTRAY_USER')
    }

    if (rootProject.file('local.properties').exists()) {
        Properties properties = new Properties()
        properties.load(rootProject.file('local.properties').newDataInputStream())
        return properties.getProperty('bintray.user')
    }
}

ext.getBintrayKey = { ->
    if (System.getenv('BINTRAY_KEY')) {
        return System.getenv('BINTRAY_KEY')
    }

    if (rootProject.file('local.properties').exists()) {
        Properties properties = new Properties()
        properties.load(rootProject.file('local.properties').newDataInputStream())
        return properties.getProperty('bintray.key')
    }
}

task cleanDeployment {
    doLast {
        logger.lifecycle('Deployment: Cleaning...')
        logger.lifecycle('Deleting: ' + project.buildDir)
        delete project.buildDir
    }
}

task buildDeployment {
    shouldRunAfter(cleanDeployment)
    finalizedBy assemble

    doFirst {
        android.variantFilter { variant ->
            if (variant.buildType.name == 'debug') {
                variant.setIgnore(true)
            }
        }
    }
}

task deploySnapshot {
    shouldRunAfter(buildDeployment)

    dependsOn cleanDeployment
    dependsOn buildDeployment
    finalizedBy bintrayUpload

    doLast {
        version = version + '-Snapshot'
        bintrayUpload.setVersionName(bintrayUpload.getVersionName() + '-Snapshot')
        bintrayUpload.setUserOrg('camerakit')
        bintrayUpload.setRepoName('camerakit-android-snapshots')
        bintrayUpload.setOverride(true)
        bintrayUpload.setPublish(true)

        logger.lifecycle('Deploying version ' + bintrayUpload.getVersionName() + ' in ' + bintrayUpload.getRepoName())
    }
}

task deployRelease {
    shouldRunAfter(buildDeployment)

    dependsOn cleanDeployment
    dependsOn buildDeployment
    finalizedBy bintrayUpload

    doLast {
        bintrayUpload.setUserOrg('camerakit')
        bintrayUpload.setRepoName('camerakit-android')

        logger.lifecycle('Deploying version ' + bintrayUpload.getVersionName() + ' in ' + bintrayUpload.getRepoName())
    }
}

group = 'com.camerakit'
version = '1.0.0-beta2'

install {
    repositories.mavenInstaller {
        pom.project {
            name 'camerakit-android'
            groupId 'com.camerakit'
            artifactId 'camerakit'
            packaging 'aar'
        }
    }
}

bintray {
    user = getBintrayUser()
    key = getBintrayKey()
    configurations = ['archives']
    pkg {
        name = 'camerakit-android'
        vcsUrl = 'https://github.com/CameraKit/camerakit-android.git'
        licenses = ['MIT']
        version {
            name = project.version
            released = new Date()
        }
    }
}